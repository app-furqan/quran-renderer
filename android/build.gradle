plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

// Load local properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { localProperties.load(it) }
}

android {
    namespace 'org.digitalkhatt.quran.renderer'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        consumerProguardFiles "consumer-rules.pro"
        
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -frtti -fexceptions"
                arguments "-DANDROID_STL=c++_shared",
                    "-DHARFBUZZ_DIR=${localProperties.getProperty('harfbuzz.dir', '')}",
                    "-DSKIA_DIR=${localProperties.getProperty('skia.dir', '')}",
                    "-DVMF_DIR=${localProperties.getProperty('vmf.dir', '')}"
            }
        }
        
        ndk {
            abiFilters "arm64-v8a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
}
